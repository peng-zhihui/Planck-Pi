#compdef systemd-resolve
# SPDX-License-Identifier: LGPL-2.1+

#
# This file is part of systemd.
#
# systemd is free software; you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation; either version 2.1 of the License, or
# (at your option) any later version.
#
# systemd is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with systemd; If not, see <http://www.gnu.org/licenses/>.

(( $+functions[_systemd-resolve_protocols] )) ||
_systemd-resolve_protocols() {
    local -a _protocol
    _protocol=( $(_call_program protocol ${service} --legend=no --protocol help; echo help) )
    _values 'protocol' "$_protocol[@]"
}

(( $+functions[_systemd-resolve_types] )) ||
_systemd-resolve_types() {
    local -a _type
    _type=( $(_call_program type ${service} --legend=no --type help; echo help) )
    _values 'type' "$_type[@]"
}

(( $+functions[_systemd-resolve_classes] )) ||
_systemd-resolve_classes() {
    local -a _class
    _class=( $(_call_program class ${service} --legend=no --class help; echo help) )
    _values 'class' "$_class[@]"
}

(( $+functions[_systemd-resolve_none] )) ||
_systemd-resolve_none() {
    _alternative : \
        'domain:DNS address:' \
        'address:email address:'
}

_arguments \
    {-h,--help}'[Print a short help text and exit]' \
    '--version[Print a short version string and exit]' \
    '--legend=no[Do not show headers and footers]' \
    '-4[Resolve IPv4 addresses]' \
    '-6[Resolve IPv6 addresses]' \
    {-i+,--interface=}'[Look on interface]:interface:_net_interfaces' \
    {-p+,--protocol=}'[Look via protocol]:protocol:_systemd-resolve_protocols' \
    {-t+,--type=}'[Query RR with DNS type]:type:_systemd-resolve_types' \
    {-c+,--class=}'[Query RR with DNS class]:class:_systemd-resolve_classes' \
    '--service[Resolve services]' \
    '--service-address=no[Do not resolve address for services]' \
    '--service-txt=no[Do not resolve TXT records for services]' \
    '--openpgp[Query OpenPGP public key]' \
    '--tlsa[Query TLS public key]' \
    '--cname=no[Do not follow CNAME redirects]' \
    '--search=no[Do not use search domains]' \
    '--statistics[Show resolver statistics]' \
    '--reset-statistics[Reset resolver statistics]' \
    '*::default: _systemd-resolve_none'
